#!/usr/bin/perl
use strict;
use warnings;

use DBI;
use DBD::Pg;
use Time::HiRes qw(time sleep);
use Data::Dumper;
use DateTime;

$| = 1;

my @DispatchConnect = ("dbi:Pg:", '', '', {pg_enable_utf8 => 1, sslmode => 'require', RaiseError => 1, PrintError => 0, AutoCommit => 1});
my @RunConnect      = ("dbi:Pg:", '', '', {pg_enable_utf8 => 1, sslmode => 'require', RaiseError => 0, PrintError => 0, AutoCommit => 1});
my @LISTENConnect   = ("dbi:Pg:", '', '', {pg_enable_utf8 => 1, sslmode => 'require', RaiseError => 1, PrintError => 0, AutoCommit => 1});
my $LISTENName = 'cron.Dispatch()';

sub SQL_Run {
    my $ProcessID = shift;
    die "Invalid ProcessID: $ProcessID" unless $ProcessID =~ m/^\d+$/;
    return "SELECT RunInSeconds FROM cron.Run(_ProcessID := $ProcessID)";
}

sub tprint {
    my $msg = shift;
    print DateTime->now(time_zone => 'local')->datetime() . ' ' . $msg . "\n";
}

my $Processes = {};

my $StartupTime = time();
my $DispatchDatabaseHandle = DBI->connect(@DispatchConnect) or die "Unable to connect";
my $ConnectTime = time() - $StartupTime;
my $Terminate_All_Backends = $DispatchDatabaseHandle->prepare('SELECT cron.Terminate_All_Backends()');
my $Reset_RunAtTime        = $DispatchDatabaseHandle->prepare('SELECT cron.Reset_RunAtTime()');
my $Dispatch               = $DispatchDatabaseHandle->prepare('SELECT RunProcessID, RunInSeconds, MaxProcesses, ConnectionPoolID, RetryOnError FROM cron.Dispatch()');
my $Log_Error              = $DispatchDatabaseHandle->prepare('SELECT cron.Log_Error(_ProcessID := $1, _PgBackendPID := $2, _PgErr := $3, _PgErrStr := $4, _PgState := $5, _PerlCallerInfo := $6, _RetryInSeconds := $7)');
my $Disable_Process        = $DispatchDatabaseHandle->prepare('SELECT cron.Disable_Process(_ProcessID := $1)');

unless ($DispatchDatabaseHandle->{pg_user} eq 'pgcronjob') {
    die "Not connected as the 'pgcronjob' database user!";
}

for (;;) {
    $Terminate_All_Backends->execute();
    my ($OK) = $Terminate_All_Backends->fetchrow_array();
    if ($OK == 1) {
        # all cron.Run() processes have now been terminated
        last;
    }
    tprint("Waiting for old cron processes to be terminated");
    sleep(1); # wait for processes to be terminated
}
$Reset_RunAtTime->execute();

my $LISTENDatabaseHandle = DBI->connect(@LISTENConnect) or die "Unable to connect LISTEN database handle";
$LISTENDatabaseHandle->do(qq{LISTEN "$LISTENName"}) or die "Unable to LISTEN: " . $LISTENDatabaseHandle->errstr;

tprint("PgCronJob is now running");

my $DispatchTime = time();
my $NumProcesses = {};

sub FinishDisconnect {
    my $ProcessID = shift;
    my $Process = $Processes->{$ProcessID};
    if (!defined $Process) {
        return 1; # OK, no such process
    }
    if (!defined $Process->{DatabaseHandle}) {
        return 1; # OK, no such database handle
    }
    my $RunDatabaseHandle = \$Process->{DatabaseHandle};
    my $Run               = \$Process->{Run};
    $$Run->finish;
    $$Run = undef;
    delete $Process->{Run};
    $$RunDatabaseHandle->disconnect;
    $$RunDatabaseHandle = undef;
    delete $Process->{DatabaseHandle};
    delete $Process->{PgBackendPID};
    return 1; #OK, finished and disconnected
}

# CheckErr checks if a non-true $ReturnValue was due to a database error or not.
sub CheckErr {
    my ($ReturnValue, $ProcessID) = @_;
    my ($Package, $FileName, $Line) = caller();
    if ($ReturnValue) {
        # OK since $ReturnValue evaluated to true
        return 1;
    }

    my $Process = $Processes->{$ProcessID};
    if (!defined $Process || !defined $Process->{DatabaseHandle}) {
        return 0; # error, no such process or database handle
    }

    my $RunDatabaseHandle = \$Process->{DatabaseHandle};
    my $Run               = \$Process->{Run};
    my $RunAtTime         = \$Process->{RunAtTime};
    my $RetryOnError      = \$Process->{RetryOnError};

    if (!defined($$RunDatabaseHandle->err) || $$RunDatabaseHandle->err == 0) {
        # OK since it was not a database error,
        # so the non-true $ReturnValue must have been
        # due to no rows returned or some other non-error situation
        return 1;
    }

    my $PgErr    = $Process->{DatabaseHandle}->err;
    my $PgErrStr = $Process->{DatabaseHandle}->errstr;
    my $PgState  = $Process->{DatabaseHandle}->state;

    $Log_Error->execute(
        $ProcessID,
        $Process->{DatabaseHandle}->{pg_pid},
        $PgErr,
        $PgErrStr,
        $PgState,
        "$FileName line $Line",
        $$RetryOnError
    );

    FinishDisconnect($ProcessID);

    if (defined $$RetryOnError) {
        $$RunAtTime = time() + $$RetryOnError;
    } else {
        delete $Processes->{$ProcessID};
        $Disable_Process->execute($ProcessID);
    }

    return 0;
}

sub DumpState {
    tprint("ConnectTime: $ConnectTime\n"
        . "DispatchTime: $DispatchTime\n"
        . "NumProcesses:\n"
        . Dumper($NumProcesses)
        . "Processes:\n"
        . Dumper($Processes)
    );
}

$SIG{HUP} = \&DumpState;

for (;;) {
    if (time() > $DispatchTime) {
        $Dispatch->execute();
        my ($RunProcessID, $RunInSeconds, $MaxProcesses, $ConnectionPoolID, $RetryOnError) = $Dispatch->fetchrow_array();
        if (defined $RunProcessID) {
            $Processes->{$RunProcessID} = {
                RunAtTime        => time() + $RunInSeconds,
                MaxProcesses     => $MaxProcesses,
                ConnectionPoolID => $ConnectionPoolID,
                RetryOnError     => $RetryOnError
            };
        } else {
            $DispatchTime = time()+1;
        }

    }
    foreach my $ProcessID (sort {$Processes->{$a}->{RunAtTime} <=> $Processes->{$b}->{RunAtTime}} keys %{$Processes}) {
        my $Process = $Processes->{$ProcessID};
        my $RunAtTime         = \$Process->{RunAtTime};
        my $RunASAP           = \$Process->{RunASAP};
        my $MaxProcesses      = \$Process->{MaxProcesses};
        my $ConnectionPoolID  = \$Process->{ConnectionPoolID};
        my $RetryOnError      = \$Process->{RetryOnError};
        my $RunDatabaseHandle = \$Process->{DatabaseHandle};
        my $Run               = \$Process->{Run};
        if (defined $$ConnectionPoolID && !defined $NumProcesses->{$$ConnectionPoolID}) {
            $NumProcesses->{$$ConnectionPoolID} = 0;
        }
        if ($$RunAtTime > 0) {
            if ($$RunAtTime > time() || (defined $$MaxProcesses && $$MaxProcesses ne '' && $NumProcesses->{$$ConnectionPoolID} >= $$MaxProcesses)) {
                next;
            }
            unless (defined $$RunDatabaseHandle) {
                $$RunDatabaseHandle = DBI->connect(@RunConnect) or die "Unable to connect";
                $Process->{PgBackendPID} = ${$RunDatabaseHandle}->{pg_pid};
                $$Run = ${$RunDatabaseHandle}->prepare(SQL_Run($ProcessID), {pg_async => PG_ASYNC}) or die "Unable to prepare";
            }
            CheckErr($$Run->execute(), $ProcessID) or die "Unexpected execute error";
            $$RunAtTime = 0;
            if (defined $$ConnectionPoolID) {
                $NumProcesses->{$$ConnectionPoolID}++;
            }
        } elsif ($$Run->pg_ready) {
            if (defined $$ConnectionPoolID) {
                $NumProcesses->{$$ConnectionPoolID}--;
            }
            my $Rows = $$Run->pg_result;
            CheckErr($Rows, $ProcessID) or next;
            if ($Rows > 1) {
                die "Unexpected numbers of rows: $Rows";
            }
            my ($RunInSeconds) = $$Run->fetchrow_array();
            CheckErr($RunInSeconds, $ProcessID) or next;
            if ($$Run->fetchrow_array()) {
                die "Unexpected extra row returned";
            }
            if (!defined $RunInSeconds || $RunInSeconds > $ConnectTime) {
                FinishDisconnect($ProcessID);
            }
            if (defined $RunInSeconds) {
                # We want to actually assign a time here in order to let
                # the main loop prioritize the tasks correctly.
                $$RunAtTime = time() + ($$RunASAP ? 0 : $RunInSeconds);
                $$RunASAP = undef;
            } else {
                delete $Processes->{$ProcessID};
            }
        }
    }
    while (my $NOTIFY = $LISTENDatabaseHandle->pg_notifies) {
        my ($NOTIFYName, $NOTIFYPID, $NOTIFYPayload) = @$NOTIFY;
        unless ($NOTIFYName eq $LISTENName && $NOTIFYPayload =~ m/^\d+$/) {
            die "Unexpected notification $NOTIFYName $NOTIFYPayload";
        }
        my $ProcessID = $NOTIFYPayload;
        if (!defined $Processes->{$ProcessID}) {
            # This could happen because notifications can be delayed
            # for an arbitrary amount of time.
            next;
        }
        if ($Processes->{$ProcessID}->{RunAtTime} > 0) {
            $Processes->{$ProcessID}->{RunAtTime} = time();
        } else {
            $Processes->{$ProcessID}->{RunASAP} = 1;
        }
    }
    sleep(0.001); # sleep 1 ms to avoid 100% CPU usage
}
